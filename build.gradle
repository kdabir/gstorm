plugins {
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.bintray" version '1.8.4'
    id "pl.allegro.tech.build.axion-release" version "1.9.3"
    id "com.dorongold.task-tree" version "1.3.1"
}

// Drawback in inferring version from git is project can not be built/released without git history in place.

scmVersion {
    // release using git info. tag as v<x.y.z>.
    tag {
        prefix = 'v'
        versionSeparator = ''
    }

    // Do not push using inbuilt git. use native CLI git.
    localOnly = true

    hooks {
        post { context ->
            /// Pushing tags manually thru native git client because jgit requires authentication via a pop-up
            println "pushing tag: v${context.currentVersion}"

            exec {
                commandLine 'git', 'push', 'origin', "v${context.currentVersion}"
            }
        }
    }
}

/**

 # Check current inferred version:
 $ ./gradlew currentVersion


 # Marking next version, which is non patch increment
 $ ./gradlew markNextVersion -Prelease.nextVersion=2.0.0

 # Dry Run
 $ ./gradlew release -Prelease.dryRun

 # Releasing a version
 $ ./gradlew release
 
 # Upload
 $ ./gradlew bintrayUpload
 
 */

// Important: infer project's version from scm (git), this must be configured after scmVersion block
project.version = scmVersion.version
group = 'io.github.kdabir.gstorm'
description = "Groovy Single Table ORM"


/**
 * Support Java8+ : as a library, we cannot enforce latest java version
 */
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.groovy.srcDirs = ['src']
    test.groovy.srcDirs = ['test']
}

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.codehaus.groovy:groovy-all:2.5.9'
    compile 'org.hsqldb:hsqldb:2.4.1'
    compile 'org.slf4j:slf4j-api:1.7.26'
    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.5.6'
    testCompile 'io.github.kdabir.directree:directree:0.3.0'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gstorm',
                'Implementation-Version': version,
                'Built-By': System.getProperty("user.name"),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty("java.version"),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        gstormMaven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description project.description
                    url "https://github.com/kdabir/gstorm"

                    developers {
                        developer {
                            id 'kdabir'
                            name 'Kunal Dabir'
                        }
                    }

                    scm {
                        url 'https://github.com/kdabir/gstorm'
                        connection 'scm:git://github.com/kdabir/gstorm.git'
                        developerConnection 'scm:git://github.com/kdabir/gstorm.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            } // withXml
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['gstormMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName          // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['MIT']
        vcsUrl = 'https://github.com/kdabir/gstorm'
    }
    dryRun = false // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}


test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}